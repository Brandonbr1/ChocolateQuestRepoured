buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'

version = '1.12.2-2.5.3B'
group = 'team.cqr.cqrepoured'
archivesBaseName = 'Chocolate_Quest_Repoured'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
    atDependencies
    buildDependencies

    implementation.extendsFrom(atDependencies)
}

repositories {
    // JEI
    maven { url 'https://dvs1.progwml6.com/files/maven' }
    maven { url 'https://modmaven.k-4u.nl' }

    // GeckoLib
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven' }

    maven { url 'https://www.cursemaven.com' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'

    implementation fg.deobf('software.bernie.geckolib:forge-1.12.2-geckolib:3.0.0-beta.9')

    atDependencies fg.deobf('mezz.jei:jei_1.12.2:4.16.1.301')

    buildDependencies 'curse.maven:AncientWarfare2-224602:3293318'
    buildDependencies 'curse.maven:CodeChickenLib-242818:2779848'
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'

    def atDepFileTree = files(configurations.atDependencies.collect { zipTree(it) }).getAsFileTree()
    accessTransformer = files(
        'src/main/resources/META-INF/accesstransformer.cfg',
        atDepFileTree.matching { include 'META-INF/jei_at.cfg' }
    )

    runs {
        client {
            taskName 'runClient CQR 1.12'
            environment 'MC_VERSION', '1.12.2'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
        }

        server {
            taskName 'runServer CQR 1.12'
            environment 'MC_VERSION', '1.12.2'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
        }
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'accesstransformer.cfg'
    }
}

compileJava {
    classpath = sourceSets.main.compileClasspath + configurations.buildDependencies
}
